# SPDX-FileNotice: üÖ≠üÑç1.0 This file is dedicated to the public domain using the CC0 1.0 Universal Public Domain Dedication <https://creativecommons.org/publicdomain/zero/1.0/>.
# SPDX-FileContributor: Jason Yundt <jason@jasonyundt.email> (2023‚Äì2024)
{ pkgs, lib, ... }:
{
	nixpkgs.overlays = [
		(self: super: {
			pre-commit = let
				pkgCollections = import ../pkgCollections {
					inherit pkgs lib;
				};
				# This PR fixes a bug with pre-commit:
				# <https://github.com/NixOS/nixpkgs/pull/267499>.
				# It‚Äôs been merged into Nixpkgs‚Äôs master branch,
				# but hasn‚Äôt been backported to NixOS 23.11 yet.
				# That‚Äôs why we‚Äôre using nixpkgs-unstable here.
				unstablePkgs = pkgCollections.nixpkgs-unstable;
			in unstablePkgs.pre-commit.override {
				# Some of the pre-commit hooks that I use [1]
				# require Python 3.12. We need to make
				# pre-commit use Python 3.12, or else those
				# hooks won‚Äôt work.
				#
				# [1]: <https://github.com/Jayman2000/jasons-pre-commit-hooks/>
				python3Packages = unstablePkgs.python312Packages;
			};
		})
	];
	home-manager.users.jayman = { pkgs, ... }: {
		home.packages = [ pkgs.pre-commit ];
	};

	# Normally, I would just have pre-commit download its own copy of
	# NodeJS, but on NixOS that doesn‚Äôt work. I tried installed NodeJS for
	# jayman only, but that also didn‚Äôt work.
	environment.systemPackages = [ pkgs.nodejs ];
}
